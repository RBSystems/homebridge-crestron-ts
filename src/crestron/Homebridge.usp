/*******************************************************************************************
  SIMPL+ Module Information
  (Fill in comments below)
*******************************************************************************************/
/*
Dealer Name:
System Name:
System Number:
Programmer: songzh
Comments:
*/

/*******************************************************************************************
  Compiler Directives
  (Uncomment and declare compiler directives as needed)
*******************************************************************************************/

#SYMBOL_NAME "Homebridge"
#HINT "This program communicates with a homebridge server to enable Crestron compatibility with Apple HomeKit."
#PRINT_TO_TRACE
#DEFAULT_VOLATILE
#ENABLE_STACK_CHECKING
#ENABLE_TRACE

/*******************************************************************************************
  Include Libraries
  (Uncomment and include additional libraries as needed)

  Note:  Crestron does not recommend the use of the #INCLUDEPATH directive in SIMPL+ modules
  that are to be used in Crestron Studio.
*******************************************************************************************/
#INCLUDEPATH "C:\Program Files (x86)\Crestron\Simpl\Usrsplus"
#USER_SIMPLSHARP_LIBRARY "SIMPLSharpLibrary1"


/*******************************************************************************************
  DIGITAL, ANALOG and SERIAL INPUTS and OUTPUTS
  (Uncomment and declare inputs and outputs as needed)
*******************************************************************************************/
DIGITAL_INPUT _SKIP_,_SKIP_;
DIGITAL_INPUT Light_IsOn[10];
DIGITAL_INPUT _SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_;
DIGITAL_INPUT Switch_IsOn[10];
DIGITAL_INPUT _SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_;
DIGITAL_INPUT Fan_IsHigh[5];
DIGITAL_INPUT Fan_IsMed[5];
DIGITAL_INPUT Fan_IsLow[5];
DIGITAL_INPUT Fan_IsOff[5];
DIGITAL_INPUT _SKIP_;
DIGITAL_INPUT MotionSensor_State[5];
DIGITAL_INPUT _SKIP_;
DIGITAL_INPUT OccupancySensor_State[5];
DIGITAL_INPUT _SKIP_;
DIGITAL_INPUT AcPower_State[2];
DIGITAL_INPUT _SKIP_;
DIGITAL_INPUT Ac_AutoState[2];
DIGITAL_INPUT _SKIP_;
DIGITAL_INPUT Ac_HeaterState[2];
DIGITAL_INPUT _SKIP_;
DIGITAL_INPUT Ac_CoolerState[2];
DIGITAL_INPUT _SKIP_;
DIGITAL_INPUT Television_IsMute[2];
DIGITAL_INPUT _SKIP_;
DIGITAL_INPUT Television_IsOn[2];
DIGITAL_INPUT _SKIP_;

ANALOG_INPUT DimLight_Level[10];
ANALOG_INPUT _SKIP_;
ANALOG_INPUT Get_CurrentPosition[2];
ANALOG_INPUT _SKIP_;
ANALOG_INPUT Ac_GetTargetTemperature[2];
ANALOG_INPUT _SKIP_;
ANALOG_INPUT Ac_CurrentTemperature[2];
ANALOG_INPUT _SKIP_;
ANALOG_INPUT Television_GetVolume[2];
ANALOG_INPUT _SKIP_;



DIGITAL_OUTPUT _SKIP_,_SKIP_;
DIGITAL_OUTPUT Light_On[10];
DIGITAL_OUTPUT Light_Off[10];
DIGITAL_OUTPUT Switch_On[10];
DIGITAL_OUTPUT Switch_Off[10];
DIGITAL_OUTPUT Fan_High[5];
DIGITAL_OUTPUT Fan_Med[5];
DIGITAL_OUTPUT Fan_Low[5];
DIGITAL_OUTPUT Fan_Off[5];
DIGITAL_OUTPUT _SKIP_;
DIGITAL_OUTPUT AcPower_On[2];
DIGITAL_OUTPUT _SKIP_;
DIGITAL_OUTPUT AcPower_Off[2];
DIGITAL_OUTPUT _SKIP_;
DIGITAL_OUTPUT Ac_Auto[2];
DIGITAL_OUTPUT _SKIP_;
DIGITAL_OUTPUT Ac_Heater[2];
DIGITAL_OUTPUT _SKIP_;
DIGITAL_OUTPUT Ac_Cooler[2];
DIGITAL_OUTPUT _SKIP_;
DIGITAL_OUTPUT Television_Mute[2];
DIGITAL_OUTPUT _SKIP_;
DIGITAL_OUTPUT Television_On[2];
DIGITAL_OUTPUT _SKIP_;
DIGITAL_OUTPUT Television_Off[2];
DIGITAL_OUTPUT _SKIP_;

ANALOG_OUTPUT DimLight_Set[10];
ANALOG_OUTPUT _SKIP_;
ANALOG_OUTPUT Set_Position[2];
ANALOG_OUTPUT _SKIP_;
ANALOG_OUTPUT Ac_TargetTemperature[2];
ANALOG_OUTPUT _SKIP_;
ANALOG_OUTPUT Television_SetVolume[2];
ANALOG_OUTPUT _SKIP_;
ANALOG_OUTPUT Television_SetRemoteKey[2];
ANALOG_OUTPUT _SKIP_;
/*******************************************************************************************
  SOCKETS
  (Uncomment and define socket definitions as needed)
*******************************************************************************************/

TCP_SERVER TCPServer[65000];

/*******************************************************************************************
  Parameters
  (Uncomment and declare parameters as needed)
*******************************************************************************************/

INTEGER_PARAMETER Port;
// SIGNED_INTEGER_PARAMETER
// LONG_INTEGER_PARAMETER
// SIGNED_LONG_INTEGER_PARAMETER
STRING_PARAMETER IPAddress[16];

/*******************************************************************************************
  Parameter Properties
  (Uncomment and declare parameter properties as needed)
*******************************************************************************************/

#BEGIN_PARAMETER_PROPERTIES IPAddress
	propValidUnits = unitString;
	propDefaultValue = "0.0.0.0";
	propShortDescription = "IP Address";
	#BEGIN_PROP_FULL_DESCRIPTION "TCP server's IP Address to allow connections from" #END_PROP_FULL_DESCRIPTION
#END_PARAMETER_PROPERTIES

#BEGIN_PARAMETER_PROPERTIES Port
	propValidUnits = unitDecimal;
	propDefaultValue = 9050d;
	propShortDescription = "port";
	#BEGIN_PROP_FULL_DESCRIPTION "TCP's server port to connect to" #END_PROP_FULL_DESCRIPTION
#END_PARAMETER_PROPERTIES

/*******************************************************************************************
  Global Variables
  (Uncomment and declare global variables as needed)
  Note:  Be sure to initialize all declared STRING variables as needed
         For example, in Function Main: myString = "";
*******************************************************************************************/

SIGNED_INTEGER doServerConnected;
STRING InternalRxBuffer[65000];

/*******************************************************************************************
  Functions
  (Add any additional functions here)
  Note:  Functions must be physically placed before the location in
         the code that calls them.
*******************************************************************************************/

Function StartServer()
{
  SIGNED_INTEGER Status;
	

	Status = SocketServerStartListen(TCPServer, IPAddress, Port);

	if (status < 0) {
		Print("Error listening to %s on port %u (status: %d)\n", IPAddress, Port, Status);
	} else {
        Print("Server started to %s on port %u (status: %d)\n", IPAddress, Port, status);
	}
}

Function SocketTx(STRING ServerTx)
{
	SIGNED_INTEGER Status;

    Status = SocketSend(TCPServer, ServerTx);

    if (Status < 0) {
		Print("Error Sending to TCPServer: %d\n", Status);
    } else {
		// Print("Sent %s\n", ServerTx);
	}
}

Function ResponseMsg(Message msg)
{
	STRING ServerTx[500];
	msg.MessageType = "Response";
	ServerTx = MessageBroker.SerializeMessage(msg) + "||";
	SocketTx(ServerTx);
}

Function EventMsg(Message msg)
{
	STRING ServerTx[500];
	ServerTx = MessageBroker.SerializeMessage(msg) + "||";
	SocketTx(ServerTx);
}

EventHandler LightSwitchMessageHandler(Message msg)
{
	INTEGER IsOn;
	INTEGER Value;
	IsOn = Light_IsOn[msg.DeviceId];

	if (msg.MessageType = "Request" && msg.Operation = "Get" && msg.Property = "Power")
	{
		Value = IsOn;
		msg.Value = Value;
		ResponseMsg(msg);
	}

	if (msg.MessageType = "Request" && msg.Operation = "Set" && msg.Property = "Power")
	{
		Value = msg.Value;

		if (Value = 1 && IsOn = 0)
		{
			PULSE(1, Light_On[msg.DeviceId]);
		}
		else if (Value = 0 && IsOn = 1)
		{
			PULSE(1, Light_Off[msg.DeviceId]);
		}
		else
		{
			ResponseMsg(msg);
		}
	}

	if (msg.MessageType = "Event" && msg.Operation = "Set" && msg.Property = "Power")
	{
			EventMsg(msg);
	}
}

EventHandler LightDimmerMessageHandler(Message msg)
{
	INTEGER IsOn;
	INTEGER Level;
	INTEGER Value;
	Level = DimLight_Level[msg.DeviceId];

	if (msg.MessageType = "Request" && msg.Operation = "Get" && msg.Property = "Power")
	{
		if (Level = 0)
		{
			IsOn = 0;				
		} else 
		{
			IsOn = 1;
		}
		Value = IsOn;
		msg.Value = Value;
		ResponseMsg(msg);
	}

	if (msg.MessageType = "Request" && msg.Operation = "Get" && msg.Property = "Level")
	{
		Value = Level;
		msg.Value = Value;
		msg.MessageType = "Response";
		ResponseMsg(msg);
	}

	if (msg.MessageType = "Request" && msg.Operation = "Set" && msg.Property = "Power")
	{
		Value = msg.Value;

		if (Value = 1 && Level = 0)
		{
			DimLight_Set[msg.DeviceId] = 65535;
		}
		else if (Value = 0 && Level > 0)
		{
			DimLight_Set[msg.DeviceId] = 0;
		}
		else
		{
			msg.MessageType = "Response";
			ResponseMsg(msg);
		}
	}

	if (msg.MessageType = "Request" && msg.Operation = "Set" && msg.Property = "Level")
	{
		Value = msg.Value;

		DimLight_Set[msg.DeviceId] = Value;
	}

	if (msg.MessageType = "Event" && msg.Operation = "Set" && msg.Property = "Level")
	{
		EventMsg(msg);
	}
}

EventHandler SwitchMessageHandler(Message msg)
{
	INTEGER IsOn;
	INTEGER Value;
	IsOn = Switch_IsOn[msg.DeviceId];

	if (msg.MessageType = "Request" && msg.Operation = "Get" && msg.Property = "Power")
	{
		Value = IsOn;
		msg.Value = Value;
		ResponseMsg(msg);
	}

	if (msg.MessageType = "Request" && msg.Operation = "Set" && msg.Property = "Power")
	{
		Value = msg.Value;

		if (Value = 1 && IsOn = 0)
		{
			PULSE(1, Switch_On[msg.DeviceId]);
		}
		else if (Value = 0 && IsOn = 1)
		{
			PULSE(1, Switch_Off[msg.DeviceId]);
		}
		else
		{
			ResponseMsg(msg);
		}
	}

	if (msg.MessageType = "Event" && msg.Operation = "Set" && msg.Property = "Power")
	{
		EventMsg(msg);
	}
}

EventHandler FanMessageHandler(Message msg)
{
	INTEGER IsOff;
	INTEGER IsLow;
	INTEGER IsMed;
	INTEGER IsHigh;
	INTEGER Value;
	IsOff = Fan_IsOff[msg.DeviceId];
	IsLow = Fan_IsLow[msg.DeviceId];
	IsMed = Fan_IsMed[msg.DeviceId];
	IsHigh = Fan_IsHigh[msg.DeviceId];

	if (msg.MessageType = "Request" && msg.Operation = "Get" && msg.Property = "Power")
	{
		Value = IsOff;

		if (Value = 1)
		{
			Value = 0;
		}
		else
		{
			Value = 1;
		}
		msg.Value = Value;
		ResponseMsg(msg);
	}

	if (msg.MessageType = "Request" && msg.Operation = "Get" && msg.Property = "PerValue")
	{
		if (IsLow = 1)
		{
			Value = 33;
		}
		if (IsMed = 1)
		{
			Value = 66;
		}
		if (IsHigh = 1)
		{
			Value = 100;
		}

		msg.Value = Value;
		msg.MessageType = "Response";
		ResponseMsg(msg);
	}

	if (msg.MessageType = "Request" && msg.Operation = "Set" && msg.Property = "Power")
	{
		Value = msg.Value;

		if (Value = 1 && IsOff = 1)
		{
			PULSE(1, Fan_High[msg.DeviceId]);
		}
		else if (Value = 0 && IsOff = 0)
		{
			PULSE(1, Fan_Off[msg.DeviceId]);
		}
		else
		{
			msg.MessageType = "Response";
			ResponseMsg(msg);
		}
	}

	if (msg.MessageType = "Request" && msg.Operation = "Set" && msg.Property = "PerValue")
	{
		Value = msg.Value;

		if (Value <= 33)
		{
			PULSE(1, Fan_Low[msg.DeviceId]);
		}
		else if (Value <= 66)
		{
			PULSE(1, Fan_Med[msg.DeviceId]);
		}
		else
		{
			PULSE(1, Fan_High[msg.DeviceId]);
		}
	}

	if (msg.MessageType = "Event" && msg.Operation = "Set" && msg.Property = "Power")
	{
		EventMsg(msg);
	}

	if (msg.MessageType = "Event" && msg.Operation = "Set" && msg.Property = "Speed")
	{
		EventMsg(msg);
	}
}

EventHandler SensorStateMessageHandler(Message msg)
{
	INTEGER IsDetected;
	INTEGER Value;

	if (msg.MessageType = "Request" && msg.Operation = "Get" && msg.Property = "State")
	{
		if (msg.DeviceType = "OccupancySensor")
		{
			IsDetected = OccupancySensor_State[msg.DeviceId];

		} else if (msg.DeviceType = "MotionSensor"){
			IsDetected = MotionSensor_State[msg.DeviceId];
		
		}
		Value = IsDetected;
		msg.Value = Value;
		ResponseMsg(msg);
	}


	if (msg.MessageType = "Event" && msg.Operation = "Set" && msg.Property = "State")
	{
		EventMsg(msg);
	}
}

EventHandler WindowCoveringHandler(Message msg)
{
	INTEGER PositionValue;
	INTEGER Value;
	STRING ServerTx[500];
	
	
	if (msg.MessageType = "Request" && msg.Operation = "Get")
	{
		
		PositionValue = Get_CurrentPosition[msg.DeviceId];
		Value = PositionValue;
		msg.Value = Value;
		ResponseMsg(msg);
	}

	if (msg.MessageType = "Request" && msg.Operation = "Set" && msg.Property = "TargetPosition")
	{
		ResponseMsg(msg);
		
		Value = msg.Value;

		Set_Position[msg.DeviceId] = Value;

	}

	if (msg.MessageType = "Event" && msg.Operation = "Set" && msg.Property = "CurrentPosition")
	{
		EventMsg(msg);
	}

}

EventHandler HeaterCoolerHandler(Message msg)
{
	INTEGER IsOn;
	INTEGER Value;
	STRING ServerTx[500];
	IsOn = AcPower_State[msg.DeviceId];

	if (msg.MessageType = "Request" && msg.Operation = "Get" && msg.Property = "Power")
	{
		Value = IsOn;
		msg.Value = Value;
		ResponseMsg(msg);
	}

	if (msg.MessageType = "Request" && msg.Operation = "Get" && msg.Property = "TargetState")
	{
		if (Ac_AutoState[msg.DeviceId] = 1)
		{
			Value = 0;
		}
		else if (Ac_CoolerState[msg.DeviceId] = 1)
		{
			Value = 2;
		}
		else if (Ac_HeaterState[msg.DeviceId] = 1)
		{
			Value = 1;
		}
		msg.Value = Value;
		ResponseMsg(msg);
	}

	if (msg.MessageType = "Request" && msg.Operation = "Get" && msg.Property = "CurrentState")
	{
		if (Ac_AutoState[msg.DeviceId] = 1)
		{
			Value = 1;
		}
		else if (Ac_CoolerState[msg.DeviceId] = 1)
		{
			Value = 3;
		}
		else if (Ac_HeaterState[msg.DeviceId] = 1)
		{
			Value = 2;
		}
		
		msg.Value = Value;
		ResponseMsg(msg);
	}

	if (msg.MessageType = "Request" && msg.Operation = "Get" && msg.Property = "CurrentTemperature")
	{
		Value = Ac_CurrentTemperature[msg.DeviceId];
		
		msg.Value = Value;
		ResponseMsg(msg);
	}

	if (msg.MessageType = "Request" && msg.Operation = "Get" && msg.Property = "TargetTemperature")
	{
		Value = Ac_GetTargetTemperature[msg.DeviceId];
		
		msg.Value = Value;
		ResponseMsg(msg);
	}

	if (msg.MessageType = "Request" && msg.Operation = "Set" && msg.Property = "Power")
	{
		Value = msg.Value;

		if (Value = 1 && IsOn = 0)
		{
			PULSE(1, AcPower_On[msg.DeviceId]);
		}
		else if (Value = 0 && IsOn = 1)
		{
			PULSE(1, AcPower_Off[msg.DeviceId]);
		}
		else
		{
			ResponseMsg(msg);
		}
	}
	
	if (msg.MessageType = "Request" && msg.Operation = "Set" && msg.Property = "TargetState")
	{
		Value = msg.Value;
		ResponseMsg(msg);

		
		if (Value = 0 )
		{
			PULSE(1, Ac_Auto[msg.DeviceId]);
		}
		else if (Value = 1)
		{
			PULSE(1, Ac_Heater[msg.DeviceId]);
		}
		else if (Value = 2)
		{
			PULSE(1, Ac_Cooler[msg.DeviceId]);
		}
		
	}

	if (msg.MessageType = "Request" && msg.Operation = "Set" && msg.Property = "TargetTemperature")
	{
		ResponseMsg(msg);
		
		Value = msg.Value;

		Ac_TargetTemperature[msg.DeviceId] = Value;

	}

	if (msg.MessageType = "Event" && msg.Operation = "Set" && msg.Property = "Power")
	{
		EventMsg(msg);
	}

	if (msg.MessageType = "Event" && msg.Operation = "Set" && msg.Property = "TargetState")
	{
		EventMsg(msg);
	}

	if (msg.MessageType = "Event" && msg.Operation = "Set" && msg.Property = "CurrentTemperature")
	{
		EventMsg(msg);
	}

	if (msg.MessageType = "Event" && msg.Operation = "Set" && msg.Property = "TargetTemperature")
	{
		EventMsg(msg);
	}
}

EventHandler TelevisionHandler(Message msg)
{
	INTEGER IsOn;
	INTEGER Value;
	IsOn = Television_IsOn[msg.DeviceId];

	if (msg.MessageType = "Request" && msg.Operation = "Get" && msg.Property = "Power")
	{
		print("%s",msg.DeviceType);
		Value = IsOn;
		msg.Value = Value;
		ResponseMsg(msg);
	}

	if (msg.MessageType = "Request" && msg.Operation = "Get" && msg.Property = "Mute")
	{
		Value = Television_IsMute[msg.DeviceId];
		msg.Value = Value;
		ResponseMsg(msg);
	}

	if (msg.MessageType = "Request" && msg.Operation = "Get" && msg.Property = "Volume")
	{
		Value = Television_GetVolume[msg.DeviceId];
		msg.Value = Value;
		ResponseMsg(msg);
	}

	if (msg.MessageType = "Request" && msg.Operation = "Set" && msg.Property = "Power")
	{
		Value = msg.Value;

		if (Value = 1 && IsOn = 0)
		{
			PULSE(1, Television_On[msg.DeviceId]);
		}
		else if (Value = 0 && IsOn = 1)
		{
			PULSE(1, Television_Off[msg.DeviceId]);
		}
		else
		{
			ResponseMsg(msg);
		}
	}

	if (msg.MessageType = "Request" && msg.Operation = "Set" && msg.Property = "Mute")
	{
		Value = msg.Value;

		if (Value = 1)
		{
			Television_Mute[msg.DeviceId] = 1;
		}
		else if (Value = 0 )
		{
			Television_Mute[msg.DeviceId] = 0;
		}
		else
		{
			ResponseMsg(msg);
		}
	}

	if (msg.MessageType = "Request" && msg.Operation = "Set" && msg.Property = "Volume")
	{
		Value = msg.Value;
		ResponseMsg(msg);
		
		Television_SetVolume[msg.DeviceId] = Value;
		
	}

	if (msg.MessageType = "Request" && msg.Operation = "Set" && msg.Property = "RemoteKey")
	{
		Value = msg.Value;
		ResponseMsg(msg);
		
		Television_SetRemoteKey[msg.DeviceId] = Value;
		
	}

	if (msg.MessageType = "Event" && msg.Operation = "Set" && msg.Property = "Power")
	{
			EventMsg(msg);
	}

	if (msg.MessageType = "Event" && msg.Operation = "Set" && msg.Property = "Mute")
	{
			EventMsg(msg);
	}

	if (msg.MessageType = "Event" && msg.Operation = "Set" && msg.Property = "Volume")
	{
			EventMsg(msg);
	}

}

Function SocketRx()
{
	STRING Temp$[65000];
	// Print("InternalRxBuffer: %s\n", InternalRxBuffer);
    While (Len(InternalRxBuffer) > 1)
	{
		Temp$ = Remove("||",InternalRxBuffer);
    	Temp$ = Left(Temp$, (Len(Temp$)-2));
		print("Temp$  %s\n",Temp$);
		MessageBroker.ParseMessage(Temp$);
	}
}

/*******************************************************************************************
  Event Handlers
  (Uncomment and declare additional event handlers as needed)
*******************************************************************************************/

CHANGE Light_IsOn
{
	Message msg;
	INTEGER Id;
	Id = GetLastModifiedArrayIndex();
	msg.MessageType = "Event";
	msg.DeviceType = "LightSwitch";
	msg.DeviceId = Id;
	msg.Operation = "Set";
	msg.Property = "Power";
	msg.Value = Light_IsOn[Id];
	MessageBroker.TriggerLightSwitchMessage(msg);
}

CHANGE DimLight_Level
{
	Message msg;
	INTEGER Id;
	Id = GetLastModifiedArrayIndex();
	msg.MessageType = "Event";
	msg.DeviceType = "LightDimmer";
	msg.DeviceId = Id;
	msg.Operation = "Set";
	msg.Property = "Level";
	msg.Value = DimLight_Level[Id];
	MessageBroker.TriggerLightDimmerMessage(msg);
}

CHANGE Switch_IsOn
{
	Message msg;
	INTEGER Id;
	Id = GetLastModifiedArrayIndex();
	msg.MessageType = "Event";
	msg.DeviceType = "Switch";
	msg.DeviceId = Id;
	msg.Operation = "Set";
	msg.Property = "Power";
	msg.Value = Switch_IsOn[Id];
	MessageBroker.TriggerSwitchMessage(msg);
}

CHANGE Fan_IsOff
{
	Message msg;
	INTEGER Id;
	Id = GetLastModifiedArrayIndex();
	msg.MessageType = "Event";
	msg.DeviceType = "Fan";
	msg.DeviceId = Id;
	msg.Operation = "Set";
	msg.Property = "Power";
	msg.Value = Fan_IsOff[Id];

	if (msg.Value = 1)
	{
		msg.Value = 0;
	}
	else
	{
		msg.Value = 1;
	}

	MessageBroker.TriggerFanMessage(msg);
}

CHANGE Fan_IsLow
{
	Message msg;
	INTEGER Id;
	Id = GetLastModifiedArrayIndex();
	if (Fan_IsLow[Id] = 1)
	{
		msg.MessageType = "Event";
		msg.DeviceType = "Fan";
		msg.DeviceId = Id;
		msg.Operation = "Set";
		msg.Property = "PerValue";
		msg.Value = 33;
		MessageBroker.TriggerFanMessage(msg);
	}
}

CHANGE Fan_IsMed
{
	Message msg;
	INTEGER Id;
	Id = GetLastModifiedArrayIndex();
	if (Fan_IsMed[Id] = 1)
	{
		msg.MessageType = "Event";
		msg.DeviceType = "Fan";
		msg.DeviceId = Id;
		msg.Operation = "Set";
		msg.Property = "PerValue";
		msg.Value = 66;
		MessageBroker.TriggerFanMessage(msg);
	}
}

CHANGE Fan_IsHigh
{
	Message msg;
	INTEGER Id;
	Id = GetLastModifiedArrayIndex();
	if (Fan_IsHigh[Id] = 1)
	{
		msg.MessageType = "Event";
		msg.DeviceType = "Fan";
		msg.DeviceId = Id;
		msg.Operation = "Set";
		msg.Property = "PerValue";
		msg.Value = 100;
		MessageBroker.TriggerFanMessage(msg);
	}
}

CHANGE MotionSensor_State
{
	Message msg;
	INTEGER Id;
	Id = GetLastModifiedArrayIndex();
	
	msg.MessageType = "Event";
	msg.DeviceType = "MotionSensor";
	msg.DeviceId = Id;
	msg.Operation = "Set";
	msg.Property = "State";
	msg.Value = MotionSensor_State[Id];
	MessageBroker.TriggerSensorStateMessage(msg);
	
}

CHANGE OccupancySensor_State
{
	Message msg;
	INTEGER Id;
	Id = GetLastModifiedArrayIndex();
	
	msg.MessageType = "Event";
	msg.DeviceType = "OccupancySensor";
	msg.DeviceId = Id;
	msg.Operation = "Set";
	msg.Property = "State";
	msg.Value = OccupancySensor_State[Id];
	MessageBroker.TriggerSensorStateMessage(msg);	
}

CHANGE Get_CurrentPosition
{
	Message msg;
	INTEGER Id;
	Id = GetLastModifiedArrayIndex();
	
	msg.MessageType = "Event";
	msg.DeviceType = "WindowCovering";
	msg.DeviceId = Id;
	msg.Operation = "Set";
	msg.Property = "CurrentPosition";
	msg.Value = Get_CurrentPosition[Id];
	MessageBroker.TriggerWindowCoveringMessage(msg);
	
}

CHANGE AcPower_State
{
	Message msg;
	INTEGER Id;
	Id = GetLastModifiedArrayIndex();
	
	msg.MessageType = "Event";
	msg.DeviceType = "HeaterCooler";
	msg.DeviceId = Id;
	msg.Operation = "Set";
	msg.Property = "Power";
	msg.Value = AcPower_State[Id];
	MessageBroker.TriggerHeaterCoolerMessage(msg);
	
}

PUSH Ac_CoolerState
{
	Message msg;
	INTEGER Id;
	Id = GetLastModifiedArrayIndex();
	
	msg.MessageType = "Event";
	msg.DeviceType = "HeaterCooler";
	msg.DeviceId = Id;
	msg.Operation = "Set";
	msg.Property = "TargetState";
	msg.Value = 2;
	MessageBroker.TriggerHeaterCoolerMessage(msg);
	
}

PUSH Ac_AutoState
{
	Message msg;
	INTEGER Id;
	Id = GetLastModifiedArrayIndex();
	
	msg.MessageType = "Event";
	msg.DeviceType = "HeaterCooler";
	msg.DeviceId = Id;
	msg.Operation = "Set";
	msg.Property = "TargetState";
	msg.Value = 0;
	MessageBroker.TriggerHeaterCoolerMessage(msg);
	
}

PUSH Ac_HeaterState
{
	Message msg;
	INTEGER Id;
	Id = GetLastModifiedArrayIndex();
	
	msg.MessageType = "Event";
	msg.DeviceType = "HeaterCooler";
	msg.DeviceId = Id;
	msg.Operation = "Set";
	msg.Property = "TargetState";
	msg.Value = 1;
	MessageBroker.TriggerHeaterCoolerMessage(msg);
	
}

CHANGE Ac_CurrentTemperature
{
	Message msg;
	INTEGER Id;
	Id = GetLastModifiedArrayIndex();
	
	msg.MessageType = "Event";
	msg.DeviceType = "HeaterCooler";
	msg.DeviceId = Id;
	msg.Operation = "Set";
	msg.Property = "CurrentTemperature";
	msg.Value = Ac_CurrentTemperature[msg.DeviceId];
	MessageBroker.TriggerHeaterCoolerMessage(msg);
	
}

CHANGE Ac_GetTargetTemperature
{
	Message msg;
	INTEGER Id;
	Id = GetLastModifiedArrayIndex();
	
	msg.MessageType = "Event";
	msg.DeviceType = "HeaterCooler";
	msg.DeviceId = Id;
	msg.Operation = "Set";
	msg.Property = "TargetTemperature";
	msg.Value = Ac_GetTargetTemperature[msg.DeviceId];
	MessageBroker.TriggerHeaterCoolerMessage(msg);
}

CHANGE Television_GetVolume
{
	Message msg;
	INTEGER Id;
	Id = GetLastModifiedArrayIndex();
	
	msg.MessageType = "Event";
	msg.DeviceType = "Television";
	msg.DeviceId = Id;
	msg.Operation = "Set";
	msg.Property = "Volume";
	msg.Value = Television_GetVolume[msg.DeviceId];
	MessageBroker.TriggerTelevisionMessage(msg);
	
}

CHANGE Television_IsMute
{
	Message msg;
	INTEGER Id;
	Id = GetLastModifiedArrayIndex();
	
	msg.MessageType = "Event";
	msg.DeviceType = "Television";
	msg.DeviceId = Id;
	msg.Operation = "Set";
	msg.Property = "Mute";
	msg.Value = Television_IsMute[msg.DeviceId];
	MessageBroker.TriggerTelevisionMessage(msg);
	
}

CHANGE Television_IsOn
{
	Message msg;
	INTEGER Id;
	Id = GetLastModifiedArrayIndex();
	
	msg.MessageType = "Event";
	msg.DeviceType = "Television";
	msg.DeviceId = Id;
	msg.Operation = "Set";
	msg.Property = "Power";
	msg.Value = Television_IsOn[msg.DeviceId];
	MessageBroker.TriggerTelevisionMessage(msg);
	
}


SOCKETCONNECT TCPServer
{
    doServerConnected = 1;
    Print("OnConnect: input buffer size is: %d\n", Len(TCPServer.SocketRxBuf));
}



SOCKETDISCONNECT TCPServer
{
    doServerConnected = 0;
}



SOCKETRECEIVE TCPServer
{
    InternalRxBuffer = InternalRxBuffer + TCPServer.SocketRxBuf;
	ClearBuffer(TCPServer.SocketRxBuf);
	SocketRx();
}



SOCKETSTATUS TCPServer
{
    SIGNED_INTEGER Status;
    Status = SocketGetStatus();
    Print("The SocketGetStatus returns:       %d\n", Status);
    Print("The TCPServer.SocketStatus returns: %d\n", TCPServer.SocketStatus);
}


/*******************************************************************************************
  Main()
  Uncomment and place one-time startup code here
  (This code will get called when the system starts up)
*******************************************************************************************/

Function Main()
{
	RegisterEvent(MessageBroker, OnLightSwitchMessage, LightSwitchMessageHandler);
	RegisterEvent(MessageBroker, OnLightDimmerMessage, LightDimmerMessageHandler);
	RegisterEvent(MessageBroker, OnSwitchMessage, SwitchMessageHandler);
	RegisterEvent(MessageBroker, OnFanMessage, FanMessageHandler);
	RegisterEvent(MessageBroker, OnSensorStateMessage, SensorStateMessageHandler);
	RegisterEvent(MessageBroker, OnWindowCoveringMessage, WindowCoveringHandler);
	RegisterEvent(MessageBroker, OnHeaterCoolerMessage, HeaterCoolerHandler);
	RegisterEvent(MessageBroker, OnTelevisionMessage, TelevisionHandler);
    WaitForInitializationComplete();
    // If you are reading any Input or Output variables, uncomment
    //   the WaitForInitializationComplete statement above and read
    //   them afterwards.  Input/Output variables will not have
    //   their correct values set until after the logic processor
    //   runs and propagates the values to them.

	//Initialize the TCP server
	StartServer();
    Print("Program started!!!.\n");
}
